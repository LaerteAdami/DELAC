DefineConstant[
length = {10, Name "Channel length"}
front_distance = {3, Name "Cylinder center distance to inlet"}
bottom_distance = {2.5, Name "Cylinder center distance from bottom"}
width = {5, Name "Channel width"}
square_length = {1, Name "Square side length"}
cylinder_size = {0.2, Name "Mesh size on cylinder"}
box_size = {0.5, Name "Mesh size on wall"}
outer_length = {30, Name "Domain length"}
coarse_size = {0.75, Name "Mesh size on outer domain"}
];

// Seed the cylinder

p = newp;
Point(p) = {-square_length/2, -square_length/2, 0, cylinder_size};
Point(p+1) = {-square_length/2, +square_length/2, 0, cylinder_size};
Point(p+2) = {+square_length/2, +square_length/2, 0, cylinder_size};
Point(p+3) = {+square_length/2, -square_length/2, 0, cylinder_size};

l = newl;
Line(l) = {p, p+1};
Line(l+1) = {p+1, p+2};
Line(l+2) = {p+2, p+3};
Line(l+3) = {p+3, p};

cylinder[] = {l, l+1, l+2, l+3};
Physical Line(1) = {cylinder[]};

// The chanel
p = newp;
Point(p) = {-front_distance, -bottom_distance, 0, box_size};
Point(p+1) = {-front_distance+length, -bottom_distance, 0, box_size};
Point(p+2) = {-front_distance+length, -bottom_distance+width, 0, box_size};
Point(p+3) = {-front_distance, -bottom_distance+width, 0, box_size};

l = newl;
// Bottom line
Line(l) = {p, p+1};
Line(l+1) = {p+1, p+2};
Line(l+2) = {p+2, p+3};
Line(l+3) = {p+3, p};

// Physical Line(2) = {l};
// Physical Line(3) = {l+1};
// Physical Line(2) += {l+2};
// Physical Line(4) = {l+3};

// Box domain
cframe[] = {l, l+1, l+2, l+3};

// The surface to be mesh;
outer = newll;
Line Loop(outer) = {cframe[]};

inner = newll;
Line Loop(inner) = {cylinder[]};

s = news;
Plane Surface(s) = {inner, outer};
Physical Surface(1) = {s};

// Outer Domain
p = newp;
Point(p) = {-outer_length/2, -outer_length/2, 0, coarse_size};
Point(p+1) = {+outer_length/2, -outer_length/2, 0, coarse_size};
Point(p+2) = {+outer_length/2, +outer_length/2, 0, coarse_size};
Point(p+3) = {-outer_length/2, +outer_length/2, 0, coarse_size};

l = newl;
// Bottom line
Line(l) = {p, p+1};
Physical Line(2) = {l};

// Outflow
Line(l+1) = {p+1, p+2};
Physical Line(3) = {l+1};

// Top no slip wall
Line(l+2) = {p+2, p+3};
Physical Line(2) += {l+2};

// Inlet
Line(l+3) = {p+3, p};
Physical Line(4) = {l+3};

// Outer domain
cframe[] = {l, l+1, l+2, l+3};

// // The surface to be mesh;
outer_outer = newll;
Line Loop(outer_outer) = {cframe[]};

s = news;
Plane Surface(s) = {outer, outer_outer};
Physical Surface(1) += {s};


//Characteristic Length{cylinder[]} = cylinder_size;
//Characteristic Length{cframe[]} = box_size;