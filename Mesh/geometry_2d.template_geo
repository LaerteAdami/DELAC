DefineConstant[
length = {10, Name "Channel length"}
front_distance = {3, Name "Cylinder center distance to inlet"}
bottom_distance = {2.5, Name "Cylinder center distance from bottom"}
width = {5, Name "Channel width"}
square_length = {1, Name "Square side length"}
cylinder_size = {0.2, Name "Mesh size on cylinder"}
box_size = {0.5, Name "Mesh size on wall"}
outer_length = {30, Name "Domain length"}
coarse_size = {0.75, Name "Mesh size on outer domain"}
beta = {0, Name "Angle"}
flap_len = {1, Name "Flap length"}
flap_width = {0.1, Name "Flap width"}
];

// Seed the cylinder
p = newp;
Point(p) = {-square_length/2, -square_length/2, 0, cylinder_size};
Point(p+1) = {+square_length/2 -flap_width * Sin(beta), -square_length/2, 0, cylinder_size};
Point(p+2) = {+square_length/2 -flap_width * Sin(beta) + flap_len*Cos(beta) , -square_length/2 - flap_len*Sin(beta), 0, cylinder_size};
Point(p+3) = {+square_length/2 + flap_len*Cos(beta), -square_length/2 + flap_width * (1 - Sin(beta)) - flap_len*Sin(beta) , 0, cylinder_size};
Point(p+4) = {+square_length/2, -square_length/2 + flap_width * (1 - Sin(beta)), 0, cylinder_size};
Point(p+5) = {+square_length/2, +square_length/2 - flap_width * (1 - Sin(beta)), 0, cylinder_size};
Point(p+6) = {+square_length/2 + flap_len*Cos(beta) , +square_length/2 - flap_width * (1 - Sin(beta)) + flap_len*Sin(beta) , 0, cylinder_size};
Point(p+7) = {+square_length/2 -flap_width * Sin(beta)+ flap_len*Cos(beta) , +square_length/2 + flap_len*Sin(beta) , 0, cylinder_size};
Point(p+8) = {+square_length/2 -flap_width * Sin(beta), +square_length/2, 0, cylinder_size};
Point(p+9) = {-square_length/2, +square_length/2, 0, cylinder_size};

l = newl;
Line(l) = {p, p+1};
Line(l+1) = {p+1, p+2};
Line(l+2) = {p+2, p+3};
Line(l+3) = {p+3, p+4};
Line(l+4) = {p+4, p+5};
Line(l+5) = {p+5, p+6};
Line(l+6) = {p+6, p+7};
Line(l+7) = {p+7, p+8};
Line(l+8) = {p+8, p+9};
Line(l+9) = {p+9, p};

cylinder[] = {l, l+1, l+2, l+3, l+4, l+5, l+6, l+7, l+8, l+9};
Physical Line(1) = {cylinder[]};

// FLAPS
Line(l+10) = {p+4, p+1};
flap_d[] = {l+1, l+2, l+3, l+10};
flap_down = newll;
Line Loop(flap_down) = {flap_d[]};
s = news;
Plane Surface(s) = {flap_down};
Physical Surface(1) = {s};


Line(l+11) = {p+8, p+5};
flap_u[] = {l+5, l+6, l+7, l+11};
flap_up = newll;
Line Loop(flap_up) = {flap_u[]};
s = news;
Plane Surface(s) = {flap_up};
Physical Surface(1) += {s};

// The chanel
p = newp;
Point(p) = {-front_distance, -bottom_distance, 0, box_size};
Point(p+1) = {-front_distance+length, -bottom_distance, 0, box_size};
Point(p+2) = {-front_distance+length, -bottom_distance+width, 0, box_size};
Point(p+3) = {-front_distance, -bottom_distance+width, 0, box_size};

l = newl;
// Bottom line
Line(l) = {p, p+1};
Line(l+1) = {p+1, p+2};
Line(l+2) = {p+2, p+3};
Line(l+3) = {p+3, p};

// Physical Line(2) = {l};
// Physical Line(3) = {l+1};
// Physical Line(2) += {l+2};
// Physical Line(4) = {l+3};

// Box domain
cframe[] = {l, l+1, l+2, l+3};

// The surface to be mesh;
outer = newll;
Line Loop(outer) = {cframe[]};

inner = newll;
Line Loop(inner) = {cylinder[]};

s = news;
Plane Surface(s) = {inner, outer};
Physical Surface(1) += {s};

// Outer Domain
p = newp;
Point(p) = {-outer_length/2, -outer_length/2, 0, coarse_size};
Point(p+1) = {+outer_length/2, -outer_length/2, 0, coarse_size};
Point(p+2) = {+outer_length/2, +outer_length/2, 0, coarse_size};
Point(p+3) = {-outer_length/2, +outer_length/2, 0, coarse_size};

l = newl;
// Bottom line
Line(l) = {p, p+1};
Physical Line(2) = {l};

// Outflow
Line(l+1) = {p+1, p+2};
Physical Line(3) = {l+1};

// Top no slip wall
Line(l+2) = {p+2, p+3};
Physical Line(2) += {l+2};

// Inlet
Line(l+3) = {p+3, p};
Physical Line(4) = {l+3};

// Outer domain
cframe[] = {l, l+1, l+2, l+3};

// // The surface to be mesh;
outer_outer = newll;
Line Loop(outer_outer) = {cframe[]};

s = news;
Plane Surface(s) = {outer, outer_outer};
Physical Surface(1) += {s};

// solid
//solid = newll;
//Line Loop(solid) = {-cylinder[]};
//s = news;
//Plane Surface(s) = {solid};
//Physical Surface(1) += {s};
//Physical Line(1) = {cylinder[]};

//Characteristic Length{cylinder[]} = cylinder_size;
//Characteristic Length{cframe[]} = box_size;
